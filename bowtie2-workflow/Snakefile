# Copyright (c) Tuukka Norri 2024
# Licenced under the MIT licence.

# vim: syntax=snakefile


# Configuration keys
# ––––––––––––––––––
# output_root
# reference_fasta
# known_variants_prefix
# known_variants_suffix
# chromosomes
# founder_count
# founder_minimum_component_distance
# alignment_id # e.g. sample identifier


wildcard_constraints:
	founder_count		= r"\d+",
	minimum_distance	= r"\d+"


rule all:
	input:	f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-bowtie2-f{config['founder_count']}-d{config['founder_minimum_component_distance']}.mapq-recalculated.sam.gz"


rule generate_founder_sequences:
	message:				"Generating founder sequences"
	conda:					"environment.yaml"
	benchmark:				f"{config['output_root']}/benchmark/panvc3/vcf2multialign.{{chromosome}}.f{{founder_count}}.d{{minimum_distance}}"
	input:
		reference			= config["reference_fasta"],
		variants			= f"{config['known_variants_prefix']}{{chromosome}}{config['known_variants_suffix']}"
	output:
		founders_a2m		= f"{config['output_root']}/panvc3/founder-sequences/chromosome.{{chromosome}}.f{{founder_count}}.d{{minimum_distance}}.a2m.gz",
		overlaps			= f"{config['output_root']}/panvc3/founder-sequences/overlaps.{{chromosome}}.f{{founder_count}}.d{{minimum_distance}}.tsv"
	shell:
		"vcf2multialign"
		" --founder-sequences={wildcards.founder_count}"
		" --minimum-distance={wildcards.minimum_distance}"
		" --input-reference={input.reference}"
		" --reference-sequence={wildcards.chromosome}"
		" --input-variants={input.variants}"
		" --chromosome={wildcards.chromosome}"
		" --output-sequences-a2m={output.founders_a2m}"
		" --dst-chromosome={wildcards.chromosome}"
		" --pipe=scripts/run-gzip.sh"
		" --output-overlaps={output.overlaps}"


rule filter_reference:
	message:				"Extracting remaining contigs from the reference"
	conda:					"environment.yaml"
	benchmark:				f"{config['output_root']}/benchmark/panvc3/filter_reference"
	threads:				8
	input:
		reference			= config["reference_fasta"],
	output:
		remaining_contigs	= f"{config['output_root']}/panvc3/founder-sequences/remaining-contigs.fa.gz",
		contig_list			= f"{config['output_root']}/panvc3/founder-sequences/contig-list.txt",
	params:
		chromosome_args		= lambda _: " ".join(map(lambda x: f"-c {x}", config["chromosomes"]))
	shell:
		"python3 scripts/filter_reference.py"
		" -o {output.contig_list}"
		" -d REF"
		" {params.chromosome_args}"
		" < {input.reference}"
		" | gzip > {output.remaining_contigs}"


rule combine_indexing_input:
	message:				"Combining reference inputs"
	benchmark:				f"{config['output_root']}/benchmark/panvc3/combine_indexing_input.f{{founder_count}}.d{{minimum_distance}}"
	threads:				8
	input:
		founder_sequences	= expand("{output_root}/panvc3/founder-sequences/chromosome.{chromosome}.f{{founder_count}}.d{{minimum_distance}}.a2m.gz", output_root = config['output_root'], chromosome = config['chromosomes']),
		remaining_contigs	= f"{config['output_root']}/panvc3/founder-sequences/remaining-contigs.fa.gz"
	output:					
		combined_contigs	= f"{config['output_root']}/panvc3/founder-sequences/indexing-input.f{{founder_count}}.d{{minimum_distance}}.a2m.gz"
	shell:
		# We don't currently need indexable output, hence we can just concatenate the files. (See also filter_reference.)
		"cat {input.founder_sequences} {input.remaining_contigs} > {output.combined_contigs}"


rule index_msa:
	message:				"Building the MSA index"
	conda:					"environment.yaml"
	benchmark:				f"{config['output_root']}/benchmark/panvc3/index_msa.f{{founder_count}}.d{{minimum_distance}}"
	input:					f"{config['output_root']}/panvc3/founder-sequences/indexing-input.f{{founder_count}}.d{{minimum_distance}}.a2m.gz"
	output:
		index				= f"{config['output_root']}/panvc3/msa-index/msa-index.f{{founder_count}}.d{{minimum_distance}}.dat",
		unaligned_fasta		= f"{config['output_root']}/panvc3/msa-index/unaligned.f{{founder_count}}.d{{minimum_distance}}.fa"
	shell:
		"panvc3_index_msa"
		" --build-index"
		" --sequences={input}"
		" --msa-index-output={output.index}"
		" --output-fasta"
		" --pipe-input='gzip -d -c' > {output.unaligned_fasta}"


rule index_bowtie2:
	message:	"Indexing the reference for Bowtie 2"
	conda:		"environment.yaml"
	benchmark:	f"{config['output_root']}/benchmark/panvc3/index_bowtie2.f{{founder_count}}.d{{minimum_distance}}"
	threads:	workflow.cores
	input:		f"{config['output_root']}/panvc3/msa-index/unaligned.f{{founder_count}}.d{{minimum_distance}}.fa"
	output:		multiext(f"{config['output_root']}/index/panvc3/bowtie2/index.f{{founder_count}}.d{{minimum_distance}}", ".1.bt2l", ".2.bt2l", ".3.bt2l", ".4.bt2l", ".rev.1.bt2l", ".rev.2.bt2l")
	shell:		f"bowtie2-build --threads {{threads}} --large-index {{input}} {config['output_root']}/index/panvc3/bowtie2/index.f{{wildcards.founder_count}}.d{{wildcards.minimum_distance}}"


rule align_reads_bowtie2:
	message:			"Aligning reads with Bowtie 2"
	conda:				"environment.yaml"
	benchmark:			f"{config['output_root']}/benchmark/panvc3/align.{config['alignment_id']}.bowtie2.f{{founder_count}}.d{{minimum_distance}}"
	threads:			workflow.cores
	input:
		index			= multiext(f"{config['output_root']}/index/panvc3/bowtie2/index.f{{founder_count}}.d{{minimum_distance}}", ".1.bt2l", ".2.bt2l", ".3.bt2l", ".4.bt2l", ".rev.1.bt2l", ".rev.2.bt2l"),
		reads_1			= config['reads_1'],
		reads_2			= config['reads_2']
	output:				f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-bowtie2-f{{founder_count}}-d{{minimum_distance}}.sam.gz"
	params:
		alignment_count	= lambda wildcards: 2 + int(wildcards.founder_count) # founders + reference + 1
	shell:				f"bowtie2 --threads {{threads}} -k {{params.alignment_count}} -1 {{input.reads_1}} -2 {{input.reads_2}} -x {config['output_root']}/index/panvc3/bowtie2/index.f{{wildcards.founder_count}}.d{{wildcards.minimum_distance}} | gzip > {{output}}"


rule project_alignments:
	message:	"Projecting the alignments"
	conda:		"environment.yaml"
	benchmark:	f"{config['output_root']}/benchmark/panvc3/project_alignments.{config['alignment_id']}.{{aligner}}.f{{founder_count}}.d{{minimum_distance}}"
	threads:	10
	input:		
				reference			= f"{config['reference_fasta']}.gz",
				reference_fai		= f"{config['reference_fasta']}.gz.fai",
				reference_gzi		= f"{config['reference_fasta']}.gz.gzi",
				msa_index			= f"{config['output_root']}/panvc3/msa-index/msa-index.f{{founder_count}}.d{{minimum_distance}}.dat",
				seq_output_order	= f"{config['output_root']}/panvc3/founder-sequences/contig-list.txt", # FIXME: Use .fai for this.
				alignments			= f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-{{aligner}}-f{{founder_count}}-d{{minimum_distance}}.sorted.bam"
	output:		
				alignments			= f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-{{aligner}}-f{{founder_count}}-d{{minimum_distance}}.projected.sam.gz"
	shell:		"panvc3_project_alignments"
				" --alignments={input.alignments}"
				" --msa-index={input.msa_index}"
				" --reference={input.reference}"
				" --reference-msa-id=REF"
				" --ref-id-separator=/"
				" --reference-order-input={input.seq_output_order}"
				" --record-index-tag=XI"
				" --preserve-tag=XS"
				" --preserve-tag=YS"
				" | gzip > {output.alignments}"


rule recalculate_mapq:
	message:	"Recalculating MAPQ"
	conda:		"environment.yaml"
	threads:	8
	benchmark:	f"{config['output_root']}/benchmark/panvc3/recalculate_mapq.{config['alignment_id']}.{{aligner}}.f{{founder_count}}.d{{minimum_distance}}"
	input:		f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-{{aligner}}-f{{founder_count}}-d{{minimum_distance}}.projected.qname-sorted.bam"
	output:		f"{config['output_root']}/alignments/{config['alignment_id']}.panvc3-{{aligner}}-f{{founder_count}}-d{{minimum_distance}}.mapq-recalculated.sam.gz"
	shell:		"panvc3_recalculate_mapq"
				" --alignments={input}"
				" | gzip > {output}"


rule bgzip:
	conda:		"environment.yaml"
	threads:	16
	benchmark:	f"{config['output_root']}/benchmark/bgzip/{{file}}.benchmark"
	input:		"{file}"
	output:		"{file}.gz"
	shell:		"bgzip -k -@ {threads} {input}"


rule bgzip_index:
	conda:		"environment.yaml"
	benchmark:	f"{config['output_root']}/benchmark/bgzip_index/{{file}}.benchmark"
	input:		"{file}.gz"
	output:		"{file}.gz.gzi"
	shell:		"bgzip -r {input}"


rule convert_sam_gz_to_bam:
	conda:		"environment.yaml"
	threads:	16 # workflow.cores
	benchmark:	f"{config['output_root']}/benchmark/convert_sam_gz_to_bam/{{alignments}}.benchmark"
	input:		"{alignments}.sam.gz"
	output:		"{alignments}.bam"
	shell:		"samtools view -@ {threads} -O BAM -o {output} {input}"


rule sort_bam:
	conda:		"environment.yaml"
	threads:	16 # workflow.cores
	benchmark:	f"{config['output_root']}/benchmark/sort_bam/{{alignments}}.benchmark"
	input:		"{alignments}.bam"
	output:		"{alignments}.sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -@ {threads} -o {output} {input}"


rule sort_sam:
	conda:		"environment.yaml"
	threads:	16 # workflow.cores
	benchmark:	f"{config['output_root']}/benchmark/sort_sam/{{alignments}}.benchmark"
	input:		"{alignments}.sam"
	output:		"{alignments}.sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -@ {threads} -o {output} {input}"


rule sort_sam_gz:
	message:	"Sorting the alignments"
	conda:		"environment.yaml"
	threads:	16
	benchmark:	f"{config['output_root']}/benchmark/sort_sam_gz/{{alignments}}.benchmark"
	input:		f"{{alignments}}.sam.gz"
	output:		f"{{alignments}}.sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -@ {threads} -o {output} {input}"


rule sort_by_qname_bam:
	message:	"Sorting the alignments by QNAME"
	conda:		"environment.yaml"
	threads:	16
	benchmark:	f"{config['output_root']}/benchmark/sort_by_qname_bam/{{alignments}}.benchmark"
	input:		f"{{alignments}}.bam"
	output:		f"{{alignments}}.qname-sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -n -@ {threads} -o {output} {input}"


rule sort_by_qname_bam_:
	message:	"Sorting the alignments by QNAME"
	conda:		"environment.yaml"
	threads:	16
	benchmark:	f"{config['output_root']}/benchmark/sort_by_qname_bam_/{{alignments}}.benchmark"
	input:		f"{{alignments}}.sorted.bam"
	output:		f"{{alignments}}.qname-sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -n -@ {threads} -o {output} {input}"


rule sort_by_qname_sam_gz:
	message:	"Sorting the alignments by QNAME"
	conda:		"environment.yaml"
	threads:	16
	benchmark:	f"{config['output_root']}/benchmark/sort_by_qname_sam_gz/{{alignments}}.benchmark"
	input:		f"{{alignments}}.sam.gz"
	output:		f"{{alignments}}.qname-sorted.bam"
	shell:		"scripts/set-open-file-limit.sh samtools sort -n -@ {threads} -o {output} {input}"


rule index_bam:
	conda:		"environment.yaml"
	threads:	16 # workflow.cores
	benchmark:	f"{config['output_root']}/benchmark/index_bam/{{alignments}}.benchmark"
	input:		"{alignments}.bam"
	output:		"{alignments}.bam.bai"
	shell:		"samtools index -@ {threads} {input}"


rule index_fasta_fai:
	conda:		"environment.yaml"
	benchmark:	f"{config['output_root']}/benchmark/index_fasta_fai/{{reference}}.benchmark"
	input:		"{reference}.fa"
	output:		"{reference}.fa.fai"
	shell:		"samtools faidx {input}"


rule index_fasta_gz_fai:
	conda:		"environment.yaml"
	benchmark:	f"{config['output_root']}/benchmark/index_fasta_gz_fai/{{reference}}.benchmark"
	input:		"{reference}.fa.gz"
	output:		"{reference}.fa.gz.fai"
	shell:		"samtools faidx {input}"


ruleorder: sort_bam > sort_sam_gz > sort_sam > sort_by_qname_bam > sort_by_qname_sam_gz > sort_by_qname_bam_
